apply plugin: 'application'
apply plugin: 'jacoco'

java.sourceCompatibility = 17
java.targetCompatibility = 17
if (JavaVersion.current().isJava9Compatible()) {
  compileJava.options.release.set(17)
}

sourceSets.test.resources.srcDirs += [ rootProject.file('assets').path ]

mainClassName = 'com.spacecomplexity.longboilife.headless.HeadlessLauncher'
application.setMainClass(mainClassName)
eclipse.project.name = appName + '-headless'

dependencies {
  implementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
  implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  implementation project(':core')

  testImplementation platform('org.junit:junit-bom:5.10.0')
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.mockito:mockito-core:5.11.0'
}

tasks.register('dist') {
  dependsOn 'jar'
}

jacocoTestReport {
  dependsOn test
  sourceSets sourceSets.test
  reports {
    xml.required = true
    html.required = true
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events "PASSED", "FAILED", "SKIPPED"
    exceptionFormat "full"
  }
  outputs.upToDateWhen { false }
  doFirst {
    println "Starting tests..."
  }
  doLast {
    println "Finished tests."
  }
}

tasks.register("cleanTest") {
  doLast {
    delete fileTree(dir: "$layout.buildDirectory/test-results").files
    delete fileTree(dir: "$layout.buildDirectory/test").files
  }
}
